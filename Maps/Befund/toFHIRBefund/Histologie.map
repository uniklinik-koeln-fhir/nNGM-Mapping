/// version = 0.1
/// title = "nNGM: Mapping Histologie FHIR"

map "http://uk-koeln.de/fhir/StructureMap/nNGM_Mapping_HistologieFHIR" = nNGM_Mapping_HistologieFHIR

uses "http://hl7.org/fhir/StructureDefinition/CTS_Transport" as source
uses "http://hl7.org/fhir/StructureDefinition/Bundle" as target
uses "http://hl7.org/fhir/StructureDefinition/Specimen" as target
uses "http://hl7.org/fhir/StructureDefinition/Observation" as target
uses "http://hl7.org/fhir/StructureDefinition/ServiceRequest" as target


/* ------------------------------ Bundle ---------------------------- */
group TransformBundle(source src: CTS_Transport, target bundle: Bundle)
{
    src -> bundle.id = uuid();
    src -> bundle.type = 'collection';

    src -> bundle.entry as entry then CreateSpecimenHistologie(src, entry);
    src -> bundle.entry as entry then CreateTransformDiagnosticReportHistologie(src, entry);
    src -> bundle.entry as entry then CreateObservationHistologie(src, entry);
    src -> bundle.entry as entry then CreateServiceRequestHistologie(src, entry);
}

/* ------------------------------ Check if Specimen is required ---------------------------- */
group CreateSpecimenHistologie(source src: CTS_Transport, target tgt: BackboneElement)
{
    src.operations as operations, operations.data as data then
    {
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_1601'
                                    or blockindex = 0 and groupindex = 0 and itemid = 'id_2515'
                                    or blockindex = 0 and groupindex = 0 and itemid = 'id_2516'
                                    or blockindex = 0 and groupindex = 0 and itemid = 'id_2517'" then
        {
            src -> tgt.resource = create('Specimen') as specimen then TransformSpecimenHistologie(src, specimen);
        };
    };
}

/* ------------------------------ Specimen ---------------------------- */
group TransformSpecimenHistologie(source src: CTS_Transport, target tgt: Specimen) 
{
    // Metadata
    src -> tgt.id = uuid();
    src -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Specimen/nNGM';

    // Patient reference
    src.patid as patid -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(patid, '\'Patient/\' + $this');

    src.operations as operations, operations.data as data then 
    {
        // Biopsy-ID -> biopsyId.value
         data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_1601'" then
        {
            values.value as value -> tgt.identifier = create('Identifier') as biopsieId, biopsieId.system = cast('http://uk-koeln.de/NamingSystem/nNGM/biopsienummer', 'FHIR.uri'), biopsieId.value = value;
        };

        // Eingang des Tumormaterials in der Pathologie -> receivedTime
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_2515'" then
        {
            values.value as value -> tgt.receivedTime = dateOp(value, 'dateTime');
        };

        // Eingangsnummer des Tumormaterials in der Pathologie -> accessionIdentifier:eingangsnummerPatho
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_2516'" then
        {
            values.value as value -> tgt.accessionIdentifier as identifier, identifier.system = cast('http://uk-koeln.de/NamingSystem/nNGM/eingangPathoNummer', 'FHIR.uri'), identifier.value = value;
        };

        // Tumormaterial -> type.text
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_2517'" then
        {
           values.value as value -> tgt.type as type, type.text = value, type.coding = create('Coding') as snomedGlobalPatientSet, snomedGlobalPatientSet.system = 'http://snomed.info/sct',
                                                                                        snomedGlobalPatientSet.version = 'http://snomed.info/sct/900000000000207008',
                                                                                        snomedGlobalPatientSet.code as code, code.extension as dataAbsentReason,
                                                                                        dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason', 
                                                                                        dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');
        };
    };
}

/* ------------------------------ Check if DiagnosticReport is required ---------------------------- */
group CreateTransformDiagnosticReportHistologie(source src: CTS_Transport, target tgt: BackboneElement)
{
    src.operations as operations, operations.data as data then
    {
        data.values as values where "blockindex = 5 and groupindex = 0 and itemid = 'assessment_id' or 
                                    blockindex = 0 and groupindex = 0 and itemid = 'extern_patho_case_id'" then
        {
            src -> tgt.resource = create('DiagnosticReport') as diagnosticReport then TransformDiagnosticReportHistologie(src, diagnosticReport);
        };
    };
}

/* ------------------------------ DiagnosticReport ---------------------------- */
group TransformDiagnosticReportHistologie(source src: CTS_Transport, target tgt: DiagnosticReport)
{
    // Metadata
    src -> tgt.id = uuid();
    src -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/DiagnosticReport/nNGM/befund';

    // Status
    src -> tgt.status as status, status.extension as dataAbsentReason, 
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason', 
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    // Status
    src -> tgt.code as code, code.extension as dataAbsentReason, 
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason', 
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    src.operations as operations, operations.data as data then
    {
        // Untersuchungs-ID -> identifier:befundNummer.value
        data.values as values where "blockindex = 5 and groupindex = 0 and itemid = 'assessment_id'" then
        {
            values.value as id -> tgt.identifier = create('identifier') as befundNummer,
                                                            befundNummer.system= 'http://uk-koeln.de/NamingSystem/nNGM/befundnummer',
                                                            befundNummer.value = id;
        };

        // Pathologie-Fall-ID -> identifier:externePathoBefundNummer.value
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'extern_patho_case_id'" then
        {
            values.value as id -> tgt.identifier = create('identifier') as externePathoBefundNummer,
                                                            externePathoBefundNummer.system= 'http://uk-koeln.de/NamingSystem/nNGM/externePathoBefundnummer',
                                                            externePathoBefundNummer.value = id;
        };
    };
}

/* ------------------------------ Observation ---------------------------- */
group CreateObservationHistologie(source src: CTS_Transport, target tgt: BackboneElement)
{
    src.operations as operations, operations.data as data then
    {
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_1658'
                                    or blockindex = 2 and groupindex = 0 and itemid = 'id_2469'
                                    or blockindex = 2 and groupindex = 0 and itemid = 'id_2403'
                                    or blockindex = 3 and groupindex = 0 and itemid = 'id_1286'
                                    or blockindex = 3 and groupindex = 0 and itemid = 'id_1296'
                                    or blockindex = 3 and groupindex = 0 and itemid = 'id_1294'
                                    or blockindex = 3 and groupindex = 0 and itemid = 'id_1288'
                                    or blockindex = 3 and groupindex = 0 and itemid = 'id_1292'
                                    or blockindex = 3 and groupindex = 0 and itemid = 'id_2519'" then
        {
            src -> tgt.resource = create('Observation') as observation then TransformObservation(src, observation);
        };
    };
}

group TransformObservation(source src: CTS_Transport, target tgt: Observation)
{
    // Resource information
    src -> tgt.id = uuid();
    src -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Observation/nNGM/histologie';

    // Patient reference
    src.patid as patid -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(patid, '\'Patient/\' + $this');

    // Code
    src -> tgt.code = cc('http://ncit.nci.nih.gov', 'C18000');

    //status
    src -> tgt.status as code, 
            code.extension as dataAbsentReason, 
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason', 
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    // Observation data 
    src.operations as operations, operations.data as data then 
    {
        // Klassifikation -> component:klassifikation.valueCodeableConcept
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_1658'" then
        {
            values.value as value -> tgt.component = create('CodeableConcept') as klassifikation,
                                                    klassifikation.code = cc('http://ncit.nci.nih.gov', 'C25161'),
                                                    klassifikation.valueCodeableConcept as vcc,
                                                    vcc.coding as coding,
                                                    coding.system = 'http://uk-koeln.de/fhir/ValueSet/nngm/histologie-klassifikation', 
                                                    coding.code = evaluate(value, '$this.split(\'\\t\').first()'),
                                                    coding.display = evaluate(value, '$this.split(\'\\t\').last()');
        };

        // Lokalisation -> bodysite
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_2469'" then
        {
            values.value as value -> tgt.bodySite = create('CodeableConcept') as cc, cc.coding as coding, 
            coding.system = 'http://uk-koeln.de/fhir/ValueSet/icd-o-3-bodysite',
            coding.code = evaluate(value, '$this.split(\'\\t\').first()'),
            coding.display = evaluate(value, '$this.split(\'\\t\').last()');
        };

        // Grading
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_2403'" then
        {
            values.value as value -> tgt.valueCodeableConcept = create('CodeableConcept') as cc, cc.coding as coding, 
            coding.system = 'http://uk-koeln.de/fhir/ValueSet/nngm/grading',
            coding.code = evaluate(value, '$this.split(\'\\t\').first()'),
            coding.display = evaluate(value, '$this.split(\'\\t\').last()');
        };

        
        //Wachstumsmuster bei Adenokarzinom lepidisch -> component:wachtumsmusterBeiAdenokarzinomLepidisch.valueQuantity
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_1286'" then
        {
            values.value as wachstumsmuster -> tgt.component = create('BackboneElement') as component then
            {
                wachstumsmuster -> component.valueQuantity = create('Quantity') as quantity then MapPercentageValue(wachstumsmuster, quantity);
                wachstumsmuster -> component.code = cc('urn:oid:2.16.840.1.113883.6.43.1', '8250/3');
            }; 
        };

        //Wachstumsmuster bei Adenokarzinom azinaer -> component:wachtumsmusterBeiAdenokarzinomAzinaer.valueQuantity
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_1296'" then
        {
            values.value as wachstumsmuster -> tgt.component = create('BackboneElement') as component then
            {
                wachstumsmuster -> component.valueQuantity = create('Quantity') as quantity then MapPercentageValue(wachstumsmuster, quantity);
                wachstumsmuster -> component.code = cc('urn:oid:2.16.840.1.113883.6.43.1', '8551/3');
            }; 
        };

        // 18: Wachstumsmuster bei Adenokarzinom papillaer -> component:wachtumsmusterBeiAdenokarzinomPapilaer.valueQuantity
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_1294'" then
        {
            values.value as wachstumsmuster -> tgt.component = create('BackboneElement') as component then
            {
                wachstumsmuster -> component.valueQuantity = create('Quantity') as quantity then MapPercentageValue(wachstumsmuster, quantity);
                wachstumsmuster -> component.code = cc('urn:oid:2.16.840.1.113883.6.43.1', '8260/3');
            }; 
        };
        
        //Wachstumsmuster bei Adenokarzinom mikropapillär -> component:wachtumsmusterBeiAdenokarzinomMikropapilaer.valueQuantity
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_1288'" then
        {
            values.value as wachstumsmuster -> tgt.component = create('BackboneElement') as component then
            {
                wachstumsmuster -> component.valueQuantity = create('Quantity') as quantity then MapPercentageValue(wachstumsmuster, quantity);
                wachstumsmuster -> component.code = cc('urn:oid:2.16.840.1.113883.6.43.1', '8265/3');
            }; 
        };

        //Wachstumsmuster bei Adenokarzinom solide -> component:wachtumsmusterBeiAdenokarzinomSolide.valueQuantity
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_1292'" then
        {
            values.value as wachstumsmuster -> tgt.component = create('BackboneElement') as component then
            {
                wachstumsmuster -> component.valueQuantity = create('Quantity') as quantity then MapPercentageValue(wachstumsmuster, quantity);
                wachstumsmuster -> component.code = cc('urn:oid:2.16.840.1.113883.6.43.1', '8230/3');
            }; 
        };

        //Anteil an Siegelringzellkarzinomen -> component:AnteilSiegelringzellkarzinomen.valueQuantity
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_2519'" then
        {
             values.value as wachstumsmuster -> tgt.component = create('BackboneElement') as component then
            {
                wachstumsmuster -> component.valueQuantity = create('Quantity') as quantity then MapPercentageValue(wachstumsmuster, quantity);
                wachstumsmuster -> component.code = cc('urn:oid:2.16.840.1.113883.6.43.1', '8490/3');
            }; 
        };
    };
}

/* ------------------------------ Helper function ---------------------------- */
group MapPercentageValue(source src: string, target tgt: Quantity)
{
    src -> tgt.value = src,
            tgt.system = 'http://unitsofmeasure.org',
            tgt.unit = 'percentage',
            tgt.code = '%';
}

/* ------------------------------ Check if ServiceRequest is required ---------------------------- */
group CreateServiceRequestHistologie(source src: CTS_Transport, target tgt: BackboneElement)
{
    src.operations as operations, operations.data as data then
    {
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_2520'
                                    or blockindex = 4 and groupindex = 0 and itemid = 'id_2462'
                                    or blockindex = 4 and groupindex = 0 and itemid = 'id_2521'" then
        {
            src -> tgt.resource = create('ServiceRequest') as servicerequest then TransformServiceRequest(src, servicerequest);
        };
    };
}

/* ------------------------------ ServiceRequest ---------------------------- */
group TransformServiceRequest(source src: CTS_Transport, target tgt: ServiceRequest)
{
    // Resource information
    src -> tgt.id = uuid();
    src -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/ServiceRequest/nNGM/testung';

    // intent
    src -> tgt.intent = 'proposal';

     // Subject
    src.patid as patid -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(patid, '\'Patient/\' + $this');

    // Category
    src -> tgt.category as category, 
            category.coding as coding,
            coding.code as code,
            code.extension as dataAbsentReason, 
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason', 
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');
                
    // code
    src -> tgt.code as code, 
            code.coding as coding,
            coding.code as actualCode,
            actualCode.extension as dataAbsentReason, 
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason', 
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    // authoredOn
    src -> tgt.authoredOn as code, 
            code.extension as dataAbsentReason, 
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason', 
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');
    
    src.operations as operations, operations.data as data then 
    {
        // Status
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_2520'" then
        {
            values.value as value -> tgt.status = cast(value, 'FHIR.code');
        };
    };

    src then TransformHistologieStatusExtension(src, tgt);
}

/* -------------------------------------- Create double extensions for Fast Track -------------------------------------------- */
group TransformHistologieStatusExtension(source src: CTS_Transport, target tgt: ServiceRequest)
{
    src.operations as operations, operations.data as data then
    {
        // Status des Abschlusses
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_2462'" then
        {
            values.value as value where "%tgt.status.extension.extension.where(url = 'status').exists().not()" then
            {
                value -> tgt.status as status collate, status.extension as statusAbschluss collate, statusAbschluss.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/statusAbschluss', statusAbschluss.extension as statusExtension then
                {
                    value -> statusExtension.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/NNGM/statusAbschluss',value, value);
                    value -> statusExtension.url = 'status';
                };
            };
        };

        //Datum des Abschlusses
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_2521'" then
        {
            values.value as value where "%tgt.status.extension.extension.where(url = 'datum').exists().not()" then
            {
                value -> tgt.status as status collate, status.extension as statusAbschluss collate, statusAbschluss.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/statusAbschluss', statusAbschluss.extension as datumExtension then
                {
                    value -> datumExtension.valueDate = dateOp(value, 'date');
                    value -> datumExtension.url = 'datum';
                };
            };
        };
    };
}