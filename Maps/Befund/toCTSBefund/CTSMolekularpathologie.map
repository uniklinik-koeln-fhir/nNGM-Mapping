/// version = 0.1
/// title = "nNGM: nNGM_Mapping_MolekularpathologieCTS - FHIR to CTS"

map "http://uk-koeln.de/fhir/StructureMap/nNGM_Mapping_MolekularpathologieCTS" = nNGM_Mapping_MolekularpathologieCTS

uses "http://hl7.org/fhir/StructureDefinition/Bundle" as source
uses "http://hl7.org/fhir/StructureDefinition/CTS_Transport" as target

imports "http://uk-koeln.de/fhir/StructureMap/nNGM_Mapping_Molekularpathologie_checkboxesCTS"

group TransformMolekularpathologieOperation(source src: Bundle, target tgt: BackboneElement)
{
    src -> tgt.type = 'save';
    src -> tgt.crfid = '1_MP1';

    src then TransformMolekularpathologieCheckboxes(src, tgt);

    src.entry as entry then
    {
        /*---------------------EpisodeOfCare-----------------------*/
        entry.resource as episodeofcare where "resource is EpisodeOfCare and resource.meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/EpisodeOfCare/nNGM'" then
        {
            //Untersuchung-ID
            episodeofcare -> tgt.data as data then
            {
                episodeofcare.identifier as identifier, identifier.value as value then
                {   
                    episodeofcare -> data.blockindex = 8;
                    episodeofcare -> data.groupindex = 0;
                    episodeofcare -> data.itemid = 'assessment_id';
                    value -> data.values as values, values.value = value;
                };
            };
        };

        /*---------------------ServiceRequest-----------------------*/
        entry.resource as servicerequest where "resource is ServiceRequest and resource.meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/ServiceRequest/nNGM/testung'" then
        {
            // Durchfuehrung status.code
            servicerequest -> tgt.data as data then
            {
                servicerequest.status as value where "$this = 'completed'" then
                {   
                    servicerequest -> data.blockindex = 7;
                    servicerequest -> data.groupindex = 0;
                    servicerequest -> data.itemid = 'id_2520';
                    value -> data.values as values, values.value = 'abgeschlossen';
                };

                servicerequest.status as value where "$this = 'active'" then
                {   
                    servicerequest -> data.blockindex = 7;
                    servicerequest -> data.groupindex = 0;
                    servicerequest -> data.itemid = 'id_2520';
                    value -> data.values as values, values.value = 'in Bearbeitung';
                };
            };

            // Abschluss status.extension:statusAbschluss.extension:status.valueCodeableConcept
            servicerequest -> tgt.data as data then
            {
                servicerequest.status as status, status.extension as statusabschluss, statusabschluss.extension as ext, ext.valueCodeableConcept as vcc, vcc.coding as coding, coding.code as value then
                {   
                    servicerequest -> data.blockindex = 7;
                    servicerequest -> data.groupindex = 0;
                    servicerequest -> data.itemid = 'id_2462';
                    value -> data.values as values, values.value = value;
                };
            };

            // Datum des Abschlusses status.extension:statusAbschluss.extension:status.valueCodeableConcept
            servicerequest -> tgt.data as data then
            {
                servicerequest.status as status, status.extension as statusabschluss, statusabschluss.extension as ext, ext.valueDate as value then
                {   
                    servicerequest -> data.blockindex = 7;
                    servicerequest -> data.groupindex = 0;
                    servicerequest -> data.itemid = 'id_2521';
                    value -> data.values as values, values.value = value;
                };
            };
        };

        /*---------------MET FISH OBSERVATION----------------*/
        entry.resource as observation where "resource is Observation 
                                            and resource.code.coding.code = 'MET' 
                                            and resource.category.coding.where(system = \'http://uk-koeln.de/fhir/ValueSet/obs-methods\').code = 'FISH'" then
        {
            //Date Of Assesment
            observation -> tgt.data as data then
            {
                observation.effectivePeriod as period then
                {   
                    observation -> data.blockindex = 4;
                    observation -> data.groupindex = 0;
                    observation -> data.itemid = 'id_2132';
                    period.start as dateOfAssessment -> data.values as values, values.value = dateOfAssessment;
                };
            };

            //Ergebnis
            observation -> tgt.data as data then
            {
                observation.valueCodeableConcept as cc, cc.coding as coding then
                {
                    observation -> data.blockindex = 4;
                    observation -> data.groupindex = 0;
                    observation -> data.itemid = 'id_2154';
                    coding.code as code -> data.values as values, values.value = code;
                };
            };

            //Amplifikation
            observation -> tgt.data as data then
            {
                observation.component as component then
                {   
                    component.valueCodeableConcept as cc where "code.coding.code = 'amplification-level' and code.coding.system = 'http://uk-koeln.de/fhir/CodeSystem/tbd-codes'" then
                    {
                        observation -> data.blockindex = 4;
                        observation -> data.groupindex = 0;
                        observation -> data.itemid = 'id_2155';
                        cc.coding as coding, coding.code as code -> data.values as values, values.value = code;
                    };
                };
            };

            //gezaehlte Tumorzellen
            observation -> tgt.data as data then
            {
                observation.component as component then
                {   
                    component.valueInteger as integer where "code.coding.code = 'C0007584' and code.coding.system = 'http://ncit.nci.nih.gov'" then
                    {
                        observation -> data.blockindex = 4;
                        observation -> data.groupindex = 0;
                        observation -> data.itemid = 'id_2159';
                        integer -> data.values as values, values.unit = 'count', values.value = integer;
                    };
                };
            };
        };
        /*---------------RET FISH OBSERVATION----------------*/
        entry.resource as observation where "resource is Observation 
                                            and resource.code.coding.code = 'RET' 
                                            and resource.category.coding.where(system = \'http://uk-koeln.de/fhir/ValueSet/obs-methods\').code = 'FISH'" then
        {
            //Date Of Assesment
            observation -> tgt.data as data then
            {
                observation.effectivePeriod as period then
                {   
                    observation -> data.blockindex = 5;
                    observation -> data.groupindex = 0;
                    observation -> data.itemid = 'id_2183';
                    period.start as dateOfAssessment -> data.values as values, values.value = dateOfAssessment;
                };
            };

            //Ergebnis
            observation -> tgt.data as data then
            {
                observation.valueCodeableConcept as cc, cc.coding as coding then
                {
                    observation -> data.blockindex = 5;
                    observation -> data.groupindex = 0;
                    observation -> data.itemid = 'id_2198';
                    coding.code as code -> data.values as values, values.value = code;
                };
            };

            //Positive Tumorzellen
            observation -> tgt.data as data then
            {
                observation.component as component then
                {   
                    component.valueQuantity as quantity where "code.coding.code = 'C70460' and code.coding.system = 'http://ncit.nci.nih.gov'" then
                    {
                        observation -> data.blockindex = 5;
                        observation -> data.groupindex = 0;
                        observation -> data.itemid = 'id_2199';
                        quantity.value as value -> data.values as values, values.unit = 'percent', values.value = value;
                    };
                };
            };
        };
        /*-------------Status der Untersuchungen------------------*/
        /*---------------MET----------------*/
        entry.resource as observation where "resource is Observation 
                                            and resource.code.coding.code = 'MET'" then
        {
            //Durchführung
            observation -> tgt.data as data then
            {
                observation.status as status where "%tgt.data.where(itemid = 'id_2520').exists().not()" then
                {
                    observation -> data.blockindex = 7;
                    observation -> data.groupindex = 0;
                    observation -> data.itemid = 'id_2520';
                    observation -> data.values as values, values.value = 'abgeschlossen';
                };
            };

            //Abschluss
            observation -> tgt.data as data then
            {
                observation.status as status where "%tgt.data.where(itemid = 'id_2462').exists().not()" then
                {
                    observation -> data.blockindex = 7;
                    observation -> data.groupindex = 0;
                    observation -> data.itemid = 'id_2462';
                    status.extension as extension where "$this.extension.url = 'http://uk-koeln.de/fhir/Extension/statusAbschluss'" then
                    {
                        extension.valueCodeableConcept as cc, cc.coding as coding, coding.code as code -> data.values as values, values.value = code;
                    };
                };
            };

            //Datum des Abschlusses
            observation -> tgt.data as data then
            {
                observation.effectivePeriod as effectiveperiod where "%tgt.data.where(itemid = 'id_2521').exists().not()" then
                {
                    observation -> data.blockindex = 7;
                    observation -> data.groupindex = 0;
                    observation -> data.itemid = 'id_2521';
                    effectiveperiod.end as abschlussdatum -> data.values as values, values.value = abschlussdatum;
                };
            };
        };

        /*---------------RET----------------*/
        entry.resource as observation where "resource is Observation 
                                            and resource.code.coding.code = 'RET'" then
        {
            //Durchführung
            observation -> tgt.data as data then
            {
                observation -> data.blockindex = 7;
                observation -> data.groupindex = 0;
                observation -> data.itemid = 'id_2520';
                observation -> data.values as values, values.value = 'abgeschlossen';
            };

            //Abschluss
            observation -> tgt.data as data then
            {
                observation.status as status where "%tgt.data.where(itemid = 'id_2462').exists().not()" then
                {
                    observation -> data.blockindex = 7;
                    observation -> data.groupindex = 0;
                    observation -> data.itemid = 'id_2462';
                    observation -> data.values as values, values.value = 'vollständig';
                };
            };

            //Datum des Abschlusses
            observation -> tgt.data as data then
            {
                observation.effectivePeriod as effectiveperiod where "%tgt.data.where(itemid = 'id_2521').exists().not()" then
                {
                    observation -> data.blockindex = 7;
                    observation -> data.groupindex = 0;
                    observation -> data.itemid = 'id_2521';
                    observation -> data.values as values, values.value = '2019-11-10';
                };
            };
        };
    };
}