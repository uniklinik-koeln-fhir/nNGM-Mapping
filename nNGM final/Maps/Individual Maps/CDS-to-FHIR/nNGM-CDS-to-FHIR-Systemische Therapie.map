/// version = 0.1
/// title = "nNGM_Mapping_SystemischeTherapieFHIR"

/*
    TODO
    - Missing extension.url for Anzahl der Zyklen (id_1250)
    - Missing valueSet for id_2342
    - When 'Abbruch wegen Process' option is checked in Art des Therapieendes (id_1356), it displays a field named 'Datum Progess' (blockindex = 6, groupindex = 0, itemid = 'sy_date_progress'),
        but its information is not on Seatable nor Master excel file, so it's not being mapped
    - id_1465 isn't mapping properly because it cannot override value already set by id_1246, even though it's effectivePeriod.start and id_1246 it's effective.start
*/

map "http://uk-koeln.de/fhir/StructureMap/nNGM_Mapping_SystemischeTherapieFHIR" = nNGM_Mapping_SystemischeTherapieFHIR

uses "http://hl7.org/fhir/StructureDefinition/CTS_Transport" as source
uses "http://hl7.org/fhir/StructureDefinition/MedicationAdministration" as target
uses "http://hl7.org/fhir/StructureDefinition/Patient" as target
uses "http://hl7.org/fhir/StructureDefinition/EpisodeOfCare" as target
uses "http://hl7.org/fhir/StructureDefinition/Observation" as target

group TransformBundleSystemischeTherapie(source src: CTS_Transport, target bundle: Bundle)
{
    // Check if any fields are filled in before creating the MedicationAdministration
    src -> bundle.entry as entry then CreateMedicationAdministrationSystemischeTherapie(src, entry);

    // Check if any fields are filled in before creating the Patient
    src -> bundle.entry as entry then CreatePatientSystemischeTherapie(src, entry);
    
    // Check if any fields are filled in before creating the EpisodeOfCare
    src -> bundle.entry as entry then CreateEpisodeOfCareSystemischeTherapie(src, entry);
    
    // Check if any fields are filled in before creating the Observation ECOG
    src -> bundle.entry as entry then CreateObservationEcogSystemischeTherapie(src, entry);

    // Check if any fields are filled in before creating the Observation Best Response
    src -> bundle.entry as entry then CreateObservationBestResponseSystemischeTherapie(src, entry);
}

/* ------------------------------ Check if MedicationAdministration needs to be created ---------------------------- */
group CreateMedicationAdministrationSystemischeTherapie(source src: CTS_Transport, target tgt: BackboneElement)
{
    src.operations as operations, operations.data as data then
    {
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1600'
            or blockindex = 1 and groupindex = 0 and itemid = 'id_2336'
            or blockindex = 1 and groupindex = 0 and itemid = 'id_2337'
            or blockindex = 1 and groupindex = 0 and itemid = 'id_2338'
            or blockindex = 1 and groupindex = 0 and itemid = 'id_2339'
            or blockindex = 2 and groupindex = 0 and itemid = 'id_1246'
            or blockindex = 2 and groupindex = 0 and itemid = 'id_1247'
            or blockindex = 2 and groupindex = 0 and itemid = 'id_1242'
            or blockindex = 2 and groupindex = 0 and itemid = 'id_1243'
            or blockindex = 2 and groupindex = 0 and itemid = 'id_2341'
            or blockindex = 2 and groupindex = 0 and itemid = 'id_2345'
            or blockindex = 2 and groupindex = 0 and itemid = 'id_1244'
            or blockindex = 3 and groupindex = 0 and itemid = 'id_1241'
            or blockindex = 3 and groupindex = 0 and itemid = 'id_1250'
            or blockindex = 3 and groupindex = 0 and itemid = 'id_1488'
            or blockindex = 3 and groupindex = 0 and itemid = 'id_1464'
            or blockindex = 3 and groupindex = 0 and itemid = 'id_1359'
            or blockindex = 3 and groupindex = 0 and itemid = 'id_1465'
            or blockindex = 4 and groupindex = 0 and itemid = 'id_1357'
            or blockindex = 4 and groupindex = 0 and itemid = 'id_2342'
            or blockindex = 6 and groupindex = 0 and itemid = 'id_1356'
            or blockindex = 6 and groupindex = 0 and itemid = 'id_2343'
            or blockindex = 7 and groupindex = 0 and itemid = 'id_1205'" then
        {
            src -> tgt.resource = create('MedicationAdministration') as medicationAdministration then TransformMedicationAdministrationSystemischeTherapie(src, medicationAdministration);
        };
    };
}

/* ------------------------------ MedicationAdministration ---------------------------- */
group TransformMedicationAdministrationSystemischeTherapie(source src: CTS_Transport, target tgt: MedicationAdministration)
{
    // Unique identifier
    src -> tgt.id = uuid();

    // Profile url
    src -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Procedure/nNGM/MedicationAdministration';

    // Patient: item required on simplifier
    src.patid as patid -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(patid, '\'Patient/\' + $this');

    // Category: item required on simplifier
    src ->  tgt.category as category,
            category.extension as dataAbsentReason,
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    src.operations as operations, operations.data as data then
    {
        // Diagnostikfall-ID: context.identifier
        data.values as values where "blockindex = 0 and groupindex = 0 and itemid = 'id_1600'" then
        {
            values.value as value ->    tgt.context = create('Reference') as context,
                                        context.identifier = create('Identifier') as identifier,
                                        identifier.value = value;
        };

        // Therapieinformationen: supportingInformation.display
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_2336'" then
        {
            values.value as value ->    tgt.supportingInformation as supportingInformation,
                                        supportingInformation.display = value;
        };

        // Therapieinformationen war hilfreich: supportingInformation.extension:therapieInformationen.extension:hilfreich
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_2337'" then
        {
            values.value as value where "$this.value = 'Ja'" then
            {
                value ->    tgt.supportingInformation as supportingInformation,
                            supportingInformation.extension as therapieInformationen,
                            therapieInformationen.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/therapieinformationen',
                            therapieInformationen.extension as hilfreich,
                            hilfreich.url = 'hilfreich',
                            hilfreich.valueBoolean = true;
            };

            values.value as value where "$this.value = 'Nein'" then
            {
                value ->    tgt.supportingInformation as supportingInformation,
                            supportingInformation.extension as therapieInformationen,
                            therapieInformationen.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/therapieinformationen',
                            therapieInformationen.extension as hilfreich,
                            hilfreich.url = 'hilfreich',
                            hilfreich.valueBoolean = false;
            };

            values.value as value where "$this.value = 'N/A' or $this.value = 'N\/A'" then
            {
                value ->    tgt.supportingInformation as supportingInformation,
                            supportingInformation.extension as therapieInformationen,
                            therapieInformationen.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/therapieinformationen',
                            therapieInformationen.extension as hilfreich,
                            hilfreich.url = 'hilfreich',
                            hilfreich.valueBoolean as valueBoolean,
                            valueBoolean.extension as dataAbsentReason,
                            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
                            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');
            };
        };

        // Therapieinformationen wurde umgesetzt: supportingInformation.extension:therapieInformationen.extension:umgesetzt
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_2338'" then
        {
            values.value as value where "$this.value = 'Ja'" then
            {
                values ->   tgt.supportingInformation as supportingInformation,
                            supportingInformation.extension as therapieInformationen,
                            therapieInformationen.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/therapieinformationen',
                            therapieInformationen.extension as umgesetzt,
                            umgesetzt.url = 'umgesetzt',
                            umgesetzt.valueBoolean = true;
            };

            values.value as value where "$this.value = 'Nein'" then
            {
                values ->   tgt.supportingInformation as supportingInformation,
                            supportingInformation.extension as therapieInformationen,
                            therapieInformationen.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/therapieinformationen',
                            therapieInformationen.extension as umgesetzt,
                            umgesetzt.url = 'umgesetzt',
                            umgesetzt.valueBoolean = false;

                // Map Grund für die Nichtumsetzung element because it only shows up when 'Nein' is selected
                src then MapGrundFurDieNichtumsetzungMedicationAdministration(src, tgt);
            };

            values.value as value where "$this.value = 'N/A' or $this.value = 'N\/A'" then
            {
                values ->   tgt.supportingInformation as supportingInformation,
                            supportingInformation.extension as therapieInformationen,
                            therapieInformationen.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/therapieinformationen',
                            therapieInformationen.extension as umgesetzt,
                            umgesetzt.url = 'umgesetzt',
                            umgesetzt.valueBoolean as valueBoolean,
                            valueBoolean.extension as dataAbsentReason,
                            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
                            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');
            };
        };

        // Therapiebeginn: effective.start
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_1246'" then
        {
            values.value as value -> tgt.effective = create('Period') as effective collate, effective.start = dateOp(value, 'dateTime');
        }; 

        // Therapieende: effective.end
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_1247'" then
        {
            values.value as value -> tgt.effective = create('Period') as effective collate, effective.end = dateOp(value, 'dateTime');
        };

        // Durchführende Einrichtung: performer.actor.display
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_1242'" then
        {
            values.value as value ->    tgt.performer as performer,
                                        performer.actor = create('Reference') as actor,
                                        actor.display = value;
        };

        // Therapeutische Intention: reasonCode
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_1243'" then
        {
            values.value as value where "$this.value = 'N/A' or $this.value = 'N\/A'" then
            {
                value ->    tgt.reasonCode as reasonCode,
                            reasonCode.extension as dataAbsentReason,
                            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
                            dataAbsentReason.valueCode = cast('not-applicable', 'FHIR.code');
            };

            values.value as value where "$this.value != 'N/A' and $this.value != 'N\/A'" then
            {
                value -> tgt.reasonCode = cc('http://uk-koeln.de/fhir/ValueSet/nngm/intention-strahlentherapie', value, value);
            };
        };

        // Off-Label-Behandlung: extension:offLabelTherapie.valueBoolean
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_2341'" then
        {
            values.value as value where "$this.value = 'Ja'" then
            {
                value ->    tgt.extension as offLabelTherapie,
                            offLabelTherapie.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/OffLabelTherapie',
                            offLabelTherapie.valueBoolean = true;
            };

            values.value as value where "$this.value = 'Nein'" then
            {
                value ->    tgt.extension as offLabelTherapie,
                            offLabelTherapie.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/OffLabelTherapie',
                            offLabelTherapie.valueBoolean = false;
            };
        };

        // Therapie innerhalb einer klinischen Studie: extension:studientherapie.extension:teilnahme.valueBoolean
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_1245'" then
        {
            values.value as value where "$this.value = 'Ja'" then
            {
                value ->    tgt.extension as studientherapie,
                            studientherapie.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/cancer-base/studientherapie',
                            studientherapie.extension as teilnahme,
                            teilnahme.url = 'teilnahme',
                            teilnahme.valueBoolean = true;
                
                // Map Studienname element because it only shows up when 'Ja' is selected
                src then MapStudiennameMedicationAdministration(src, tgt);
            };

            values.value as value where "$this.value = 'Nein'" then
            {
                value ->    tgt.extension as studientherapie,
                            studientherapie.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/cancer-base/studientherapie',
                            studientherapie.extension as teilnahme,
                            teilnahme.url = 'teilnahme',
                            teilnahme.valueBoolean = false;
            };
        };

        // Therapieschema: medicationValueCodeableConcept
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_1241'" then
        {
            values.value as value -> tgt.medicationCodeableConcept = create('CodeableConcept') as medicationCodeableConcept collate,
                                        medicationCodeableConcept.coding = c('http://uk-koeln.de/fhir/CodeSystem/nNGM/SystemischeTherapieMedikation', value, 'Therapieschema');
        };

        // Anzahl der Zyklen (ohne Erhaltung): dosage.extension:anzahlZyklen.valueIntegerid_1250
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_1250'" then
        {
            values.value as value ->    tgt.dosage as dosage,
                                        dosage.extension = create('BackboneElement') as anzahlZyklen,
                                        anzahlZyklen.valueInteger = cast(value, 'FHIR.integer');
        };
        
        //  Änderung des Schemas
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_1488'" then
        {
            values.value as value where "$this.value = 'yes'" then
            {
                src then MapAnderungDesSchemasElementsMedicationAdministration(src, tgt);
            };
        };

        // Erhaltungstherapie als Option: extension:Erhaltungstherapie.extension:option
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_1357'" then
        {
            values.value as value where "$this.value = 'Ja'" then
            {
                value ->    tgt.extension as erhaltungstherapie,
                            erhaltungstherapie.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/erhaltungstherapie',
                            erhaltungstherapie.extension as option,
                            option.url = 'option',
                            option.valueBoolean = true;

                src then MapErhaltungstherapieElementMedicationAdministration(src, tgt);
            };
            
            values.value as value where "$this.value = 'Nein'" then
            {
                value ->    tgt.extension as erhaltungstherapie,
                            erhaltungstherapie.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/erhaltungstherapie',
                            erhaltungstherapie.extension as option,
                            option.url = 'option',
                            option.valueBoolean = false;
            };
        };

        // Art des Therapieendes: status (+ statusReason)
        data.values as values where "blockindex = 6 and groupindex = 0 and itemid = 'id_1356'" then
        {
            values.value as value where "$this.value = 'Regul\u00e4res Ende' or $this.value = 'Reguläres Ende'" then
            {
                value -> tgt.status = 'completed';
                value -> tgt.statusReason = cc('http://uk-koeln.de/fhir/ValueSet/dummy', 'Regul\u00e4res Ende', 'Regul\u00e4res Ende');
            };

            values.value as value where "$this.value = 'Abbruch wegen Nebenwirkungen'" then
            {
                value -> tgt.status = 'stopped';
                value -> tgt.statusReason = cc('http://uk-koeln.de/fhir/ValueSet/ngnm/systemischeStatusReason', 'nebenwirkungen', 'Abbruch wegen Nebenwirkungen');
            };

            values.value as value where "$this.value = 'Pause wegen Nebenwirkungen'" then
            {
                value -> tgt.status = 'on-hold';
                value -> tgt.statusReason = cc('http://uk-koeln.de/fhir/ValueSet/ngnm/systemischeStatusReason', 'nebenwirkungen', 'Pause wegen Nebenwirkungen');
                src then MapNebenwirkungenGradMedicationAdministration(src, tgt);
            };

            values.value as value where "$this.value = 'Abbruch wegen Progress'" then
            {
                value -> tgt.status = 'stopped';
                value -> tgt.statusReason = cc('http://uk-koeln.de/fhir/ValueSet/ngnm/systemischeStatusReason', 'progress', 'Progress');
                src then MapNebenwirkungenGradMedicationAdministration(src, tgt);
            };

            values.value as value where "$this.value = 'Ablehnung durch den Patienten'" then
            {
                value -> tgt.status = 'not-done';
                value -> tgt.statusReason = cc('http://uk-koeln.de/fhir/ValueSet/ngnm/systemischeStatusReason', 'ablehnungPatient', 'Ablehnung durch den Patienten');
            };

            values.value as value where "$this.value = 'Vielversprechendere Therapieoption'" then
            {
                value -> tgt.status = 'not-done';
                value -> tgt.statusReason = cc('http://uk-koeln.de/fhir/ValueSet/ngnm/systemischeStatusReason', 'vielversprechendereTherapieoption', 'Vielversprechendere Therapieoption');
            };

            values.value as value where "$this.value = 'Patient verstorben'" then
            {
                value -> tgt.status = 'stopped';
                value -> tgt.statusReason = cc('http://uk-koeln.de/fhir/ValueSet/ngnm/systemischeStatusReason', 'verstorben', 'Patient verstorben');
            };

            values.value as value where "$this.value = 'Sonstiges'" then
            {
                value -> tgt.status = 'unknown';
                value -> tgt.statusReason = cc('http://uk-koeln.de/fhir/ValueSet/ngnm/systemischeStatusReason', 'sonstiges', 'Sonstiges');
            };

            values.value as value where "$this.value = 'N/A' or $this.value = 'N\/A'" then
            {
                value -> tgt.status = 'unknown';
                value -> tgt.statusReason = cc('http://hl7.org/fhir/StructureDefinition/data-absent-reason', 'not-applicable', 'not-applicable');
            };
        };

        // Bemerkungen: note.text
        data.values as values where "blockindex = 7 and groupindex = 0 and itemid = 'id_1205'" then
        {
            values.value as value ->    tgt.note as note,
                                        note.text = value;
        };
    };
}

/* ------------------------------ MedicationAdministration - Grund für die Nichtumsetzung ---------------------------- */
group MapGrundFurDieNichtumsetzungMedicationAdministration(source src: CTS_Transport, target tgt: MedicationAdministration)
{
    src.operations as operations, operations.data as data then
    {   
        // Grund für die Nichtumsetzung: supportingInformation.extension:therapieInformationen.extension:nichtumsetzungGrund
        data.values as values where "blockindex = 1 and groupindex = 0 and itemid = 'id_2339'" then
        {
            values.value as value where "$this.value.exists()" then
            {
                value ->    tgt.supportingInformation as supportingInformation,
                            supportingInformation.extension as therapieInformationen,
                            therapieInformationen.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/therapieinformationen',
                            therapieInformationen.extension as nichtumsetzungGrund,
                            nichtumsetzungGrund.url = 'nichtumsetzungGrund',
                            nichtumsetzungGrund.valueCodeableConcept = cc('http://uk-koeln.de/fhir/CodeSystem/nngm/SystemischeTherapieGrundNichtumsetzung', value);
            };
        };
    };
}

/* ------------------------------ MedicationAdministration - Studienname ---------------------------- */
group MapStudiennameMedicationAdministration(source src: CTS_Transport, target tgt: MedicationAdministration)
{
    src.operations as operations, operations.data as data then
    {
        // Studienname: extension:studientherapie.extension:studie
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_1244'" then
        {
            values.value as value where "$this.value.exists()" then
            {
                value ->    tgt.extension as studientherapie,
                            studientherapie.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/cancer-base/studientherapie',
                            studientherapie.extension as studie,
                            studie.url = 'studie',
                            studie.valueReference = create('Reference') as reference,
                            reference.reference = evaluate(value, '$this.split(\'\\t\').first()'),
                            reference.display = evaluate(value, '$this.split(\'\\t\').last()');
            };
        };
    };
}

/* ------------------------------ MedicationAdministration - Elements displayed when Änderung des Schemas is 'Ja' ---------------------------- */
group MapAnderungDesSchemasElementsMedicationAdministration(source src: CTS_Transport, target tgt: MedicationAdministration)
{
    src.operations as operations, operations.data as data then
    {
        // Grund für die Änderung des Therapieschemas: statusReason.text
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_1464'" then
        {
            values.value as value where "$this.value.exists()" then
            {
                value ->    tgt.statusReason as statusReason,
                            statusReason.text =  value;
            };
        };

        // Neues Therapieschema: medicationCodeableConcept
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_1359'" then
        {
            values.value as value where "$this.value.exists()" then
            {
                value ->    tgt.medicationCodeableConcept = create('CodeableConcept') as medicationCodeableConcept collate,
                            medicationCodeableConcept.coding = c('http://uk-koeln.de/fhir/CodeSystem/nNGM/SystemischeTherapieMedikation', value, 'NeuesTherapieschema');
            };
        };

        // Datum der Änderung: effectivePeriod.start
        data.values as values where "blockindex = 3 and groupindex = 0 and itemid = 'id_1465'" then
        {
            values.value as value where "$this.value.exists()" then
            {
                value ->    tgt.effectivePeriod = create('Period') as effectivePeriod,
                            effectivePeriod.start = dateOp(value, 'dateTime');
            };
        };
    };
}

/* ------------------------------ MedicationAdministration - Elements displayed when Erhaltungstherapie als Option is 'Ja' ---------------------------- */
group MapErhaltungstherapieElementMedicationAdministration(source src: CTS_Transport, target tgt: MedicationAdministration)
{
    src.operations as operations, operations.data as data then
    {
        // Für die Erhaltungstherapie verabreichtes Medikament: extension:Erhaltungstherapie.extension:medikament
        data.values as values where "blockindex = 4 and groupindex = 0 and itemid = 'id_2342'" then
        {
            values.value as value where "$this.value.exists()" then
            {
                value ->    tgt.extension as erhaltungstherapie,
                            erhaltungstherapie.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/erhaltungstherapie',
                            erhaltungstherapie.extension as medikament,
                            medikament.url = 'medikament',
                            medikament.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/dummy', value);
            };
        };
    };
}

/* ------------------------------ MedicationAdministration - Element displayed when Art des therapieendes is 'Abbruch wegen Nebenwirkungen' or 'Pause wegen Nebenwirkungen' ---------------------------- */
group MapNebenwirkungenGradMedicationAdministration(source src: CTS_Transport, target tgt: MedicationAdministration)
{
    src.operations as operations, operations.data as data then
    {
        // Nebenwirkungen Grad III / Grad IV: extension:nebenwirkung
        data.values as values where "blockindex = 6 and groupindex = 0 and itemid = 'id_2343'"then
        {
            values.value as value where "$this.value.exists() and $this.value = 'Ja'" then
            {
                value ->    tgt.extension as nebenwirkung,
                            nebenwirkung.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/Nebenwirkung',
                            nebenwirkung.valueBoolean = true;
            };

            values.value as value where "$this.value.exists() and $this.value = 'Nein'" then
            {
                value ->    tgt.extension as nebenwirkung,
                            nebenwirkung.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/Nebenwirkung',
                            nebenwirkung.valueBoolean = false;
            };

            values.value as value where "$this.value.exists() and $this.value = 'N/A' or $this.value = 'N\/A'" then
            {
                value ->    tgt.extension as nebenwirkung,
                            nebenwirkung.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/Nebenwirkung',
                            nebenwirkung.valueBoolean as valueBoolean,
                            valueBoolean.extension as dataAbsentReason,
                            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
                            dataAbsentReason.valueCode = cast('not-applicable', 'FHIR.code');
            };
        };
    };
}

/* ------------------------------ Check if Patient needs to be created ---------------------------- */
group CreatePatientSystemischeTherapie(source src: CTS_Transport, target tgt: BackboneElement)
{
    src.operations as operations, operations.data as data then
    {
        data.values as values where "blockindex = 6 and groupindex = 0 and itemid = 'id_2344'" then
        {
            src -> tgt.resource = create('Patient') as patient then TransformPatientSystemischeTherapie(src, patient); 
        };
    };
}

/* ------------------------------ Patient ---------------------------- */
group TransformPatientSystemischeTherapie(source src: CTS_Transport, target tgt: Patient)
{
    // Unique identifier
    src -> tgt.id = uuid();

    // Profile url
    src -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Patient/nNGM';

    // Name: item required on simplifier
    src ->  tgt.name as name,
            name.extension as dataAbsentReason,
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    // Gender: item required on simplifier
    src ->  tgt.gender as gender,
            gender.extension as otherAmtlich,
            otherAmtlich.url = 'http://fhir.de/StructureDefinition/gender-amtlich-de',
            otherAmtlich.extension as dataAbsentReason,
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    // BirthDate: item required on simplifier
    src ->  tgt.birthDate as birthDate,
            birthDate.extension as dataAbsentReason,
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    src.operations as operations, operations.data as data then
    {
        // Todesdatum: deceased.deceasedDateTime
        data.values as values where "blockindex = 6 and groupindex = 0 and itemid = 'id_2344'" then
        {
            values.value as value -> tgt.deceasedDateTime = dateOp(value, 'dateTime');
        };
    };
}

/* ------------------------------ Check if EpisodeOfCare needs to be created ---------------------------- */
group CreateEpisodeOfCareSystemischeTherapie(source src: CTS_Transport, target tgt: BackboneElement)
{
    src.operations as operations, operations.data as data then
    {
        data.values as values where "blockindex = 5 and groupindex = 0 and itemid = 'id_1545'" then
        {
            src -> tgt.resource = create('EpisodeOfCare') as episodeOfCare then TransformEpisodeOfCareSystemischeTherapie(src, episodeOfCare); 
        };
    };
}

/* ------------------------------ EpisodeOfCare ---------------------------- */
group TransformEpisodeOfCareSystemischeTherapie(source src: CTS_Transport, target tgt: EpisodeOfCare)
{
    // Unique identifier
    src -> tgt.id = uuid();

    // Profile url
    src -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/EpisodeOfCare/nNGM';

    // Identifier.system and Identifier.value: items required on simplifier
    src ->  tgt.identifier = create('Identifier') as nngmFallnummer,
            nngmFallnummer.system = 'http://uk-koeln.de/NamingSystem/nNGM/fallnummer',
            nngmFallnummer.value as value,
            value.extension as dataAbsentReason,
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    // Status: item required on simplifier
    src -> tgt.status = 'active';

    // Patient: item required on simplifier
    src.patid as patid -> tgt.patient = create('Reference') as subject, subject.reference = evaluate(patid, '\'Patient/\' + $this');

    src.operations as operations, operations.data as data then
    {
        //Datum Progress: EpisodeOfCare.extension:datumProgress
        data.values as values where "blockindex = 5 and groupindex = 0 and itemid = 'id_1545'" then
        {
            values.value as value ->    tgt.extension as datumProgress,
                                        datumProgress.url = 'http://uk-koeln.de/fhir/StructureDefinition/Extension/nNGM/datumProgress',
                                        datumProgress.valueDate = dateOp(value, 'date');
        };
    };
}

/* ------------------------------ Check if Observation ECOG needs to be created ---------------------------- */
group CreateObservationEcogSystemischeTherapie(source src: CTS_Transport, target tgt: BackboneElement)
{
    src.operations as operations, operations.data as data then
    {
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_2415'" then
        {
            src -> tgt.resource = create('Observation') as observation then TransformObservationEcogSystemischeTherapie(src, observation); 
        };
    };
}

/* ------------------------------ Observation ECOG ---------------------------- */
group TransformObservationEcogSystemischeTherapie(source src: CTS_Transport, target tgt: Observation)
{
    // Unique identifier
    src -> tgt.id = uuid();

    // Profile url
    src -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Observation/nNGM/ecog';

    // Status: item required on simplifier
    src -> tgt.status = 'final';

    // Category: item required on simplifier
    src -> tgt.category = cc('http://hl7.org/fhir/ValueSet/observation-category', 'survey');

    // Code: item required on simplifier
    src -> tgt.code = cc('http://loinc.org', '89247-1');

    // Patient: item required on simplifier
    src.patid as patid -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(patid, '\'Patient/\' + $this');

    src.operations as operations, operations.data as data then
    {
        // ECOG Performance Status bei Therapiebeginn: valueCodeableConcept
        data.values as values where "blockindex = 2 and groupindex = 0 and itemid = 'id_2415'" then
        {
            values.value as value where "$this.value = '0 - normale, uneingeschr\u00e4nkte Aktivit\u00e4t, wie vor der Erkrankung'" then
            {
                value -> tgt.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/ecog', '0', 'ECOG 0 (Normale, uneingeschränkte Aktivität wie vor der Erkrankung)');
            };

            values.value as value where "$this.value = '1 - Einschr\u00e4nkung bei k\u00f6rperlicher Anstrengung, gehf\u00e4hig, leichte k\u00f6rperliche Arbeit m\u00f6glich'" then
            {
                value -> tgt.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/ecog', '1', 'ECOG 1: (Einschränkung bei körperlicher Anstrengung, aber gehfähig; leichte körperliche Arbeit bzw. Arbeit im Sitzen)');
            };

            values.value as value where "$this.value = '2 - gef\u00e4hig, Selbstversorgung m\u00f6glich, aber nicht arbeitsf\u00e4hig, kann mehr als 50 % der Wachzeit aufstehen'" then
            {
                value -> tgt.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/ecog', '2', 'ECOG 2: (Gehfähig, Selbstversorgung möglich, aber nicht arbeitsfähig; kann mehr als 50% der Wachzeit aufstehen)');
            };
            
            values.value as value where "$this.value = '3 - nur begrenzte Selbstversorgung m\u00f6glich, 50 % oder mehr der Wachzeit an Bett oder Stuhl gebunden'" then
            {
                value -> tgt.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/ecog', '3', 'ECOG 3: (Nur begrenzte Selbstversorgung möglich; ist 50% oder mehr der Wachzeit an Bett oder Stuhl gebunden)');
            };
            
            values.value as value where "$this.value = '4 - v\u00f6llig pflegebed\u00fcrftig, keinerlei Selbstversorgung m\u00f6glich, v\u00f6llig an Bett oder Stuhl gebunden'" then
            {
                value -> tgt.valueCodeableConcept = cc('http://uk-koeln.de/fhir/ValueSet/ecog', '4', 'ECOG 4: (Völlig pflegebedürftig, keinerlei Selbstversorgung möglich; völlig an Bett oder Stuhl gebunden)');
            };

            values.value as value where "$this.value = 'N/A' or $this.value = 'N\/A'" then
            {
                value ->    tgt.valueCodeableConcept as valueCodeableConcept,
                            valueCodeableConcept.extension as dataAbsentReason,
                            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
                            dataAbsentReason.valueCode = cast('not-applicable', 'FHIR.code');
            };
        };
    };
}

/* ------------------------------ Check if Observation BestResponse needs to be created ---------------------------- */
group CreateObservationBestResponseSystemischeTherapie(source src: CTS_Transport, target tgt: BackboneElement)
{
    src.operations as operations, operations.data as data then
    {
        data.values as values where "blockindex = 5 and groupindex = 0 and itemid = 'id_1354'
        or blockindex = 5 and groupindex = 0 and itemid = 'id_1355'" then
        {
            src -> tgt.resource = create('Observation') as observation then TransformObservationBestResponseSystemischeTherapie(src, observation); 
        };
    };
}

/* ------------------------------ Observation BestResponse ---------------------------- */
group TransformObservationBestResponseSystemischeTherapie(source src: CTS_Transport, target tgt: Observation)
{
    // Unique identifier
    src -> tgt.id = uuid();

    // Profile url
    src -> tgt.meta as meta collate, meta.profile = 'http://uk-koeln.de/fhir/StructureDefinition/Observation/nNGM/best-response';

    // PartOf: item required on simplifier
    src ->  tgt.partOf as partOf,
            partOf.extension as dataAbsentReason,
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    // Status: item required on simplifier
    src ->  tgt.status as status,
            status.extension as dataAbsentReason,
            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
            dataAbsentReason.valueCode = cast('not-asked', 'FHIR.code');

    // Category: item required on simplifier
    src -> tgt.category = cc('http://terminology.hl7.org/CodeSystem/observation-category', 'therapy');

    // Code: item required on simplifier
    src -> tgt.code = cc('http://ncit.nci.nih.gov', 'C94536');

    // Patient: item required on simplifier
    src.patid as patid -> tgt.subject = create('Reference') as subject, subject.reference = evaluate(patid, '\'Patient/\' + $this');

    src.operations as operations, operations.data as data then
    {
        // Bestes Ansprechen: valueCodeableConcept
        data.values as values where "blockindex = 5 and groupindex = 0 and itemid = 'id_1354'" then
        {
            values.value as value where "$this.value = 'Complete Response'" then
            {
                value -> tgt.valueCodeableConcept = cc('http://uk-koeln.de/fhir/CodeSystem/nngm/SystemischeTherapieRECIST', 'CR', 'Complete Remission');
            };

            values.value as value where "$this.value = 'Partial Response'" then
            {
                value -> tgt.valueCodeableConcept = cc('http://uk-koeln.de/fhir/CodeSystem/nngm/SystemischeTherapieRECIST', 'PR', 'Partial Remission');
            };

            values.value as value where "$this.value = 'Stable Disease'" then
            {
                value -> tgt.valueCodeableConcept = cc('http://uk-koeln.de/fhir/CodeSystem/nngm/SystemischeTherapieRECIST', 'SD', 'Stable Disease');
            };

            values.value as value where "$this.value = 'Progressiv Disease'" then
            {
                value -> tgt.valueCodeableConcept = cc('http://uk-koeln.de/fhir/CodeSystem/nngm/SystemischeTherapieRECIST', 'PD', 'Progressive Disease');
            };

            values.value as value where "$this.value = 'Divergentes Geschehen (nach RECIST nicht m\u00f6glich)'" then
            {
                value -> tgt.valueCodeableConcept = cc('http://uk-koeln.de/fhir/CodeSystem/nngm/SystemischeTherapieDivergentesGeschehen', 'Divergentes Geschehen (nach RECIST nicht möglich)', 'Divergentes Geschehen (nach RECIST nicht möglich)');
            };

            values.value as value where "$this.value = 'N/A' or $this.value = 'N\/A'" then
            {
                value ->    tgt.valueCodeableConcept as valueCodeableConcept,
                            valueCodeableConcept.extension as dataAbsentReason,
                            dataAbsentReason.url = 'http://hl7.org/fhir/StructureDefinition/data-absent-reason',
                            dataAbsentReason.valueCode = cast('not-applicable', 'FHIR.code');
            };
        };

        // Beurteilung nach: method
        data.values as values where "blockindex = 5 and groupindex = 0 and itemid = 'id_1355'" then
        {
            values.value as value -> tgt.method = cc('http://uk-koeln.de/fhir/ValueSet/nngm/SystemischeTherapieBeurteilungNach', value);
        };
    };
}